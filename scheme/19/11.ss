(load "~/code/advent/load.ss")
(advent-year 19)
(advent-day 11)

(define intcode
  (parse-advent comma-separated))

(define (turn-right dir) (* dir 0-i))
(define (turn-left dir) (* dir 0+i))
(define white 1)
(define black 0)
(define (white? color) (= color white))

(define (calculate-paint M color)
  (send-input M color)
  (run-until-halt M)
  (let ((instructions (read-output M)))
    (and (not (null? instructions)) instructions)))

(define (painter M color)
  (define location 0)
  (define direction 0+i)
  (define visited (make-eqv-hashtable))
  (define colors (make-eqv-hashtable))
  (when (white? color)
    (hashtable-set! colors location white))
  (let run ()
    (let ((instructions (calculate-paint M (hashtable-ref colors location black))))
      (when instructions
        (hashtable-set! visited location #t)
        (if (white? (car instructions))
            (hashtable-set! colors location white)
            (hashtable-delete! colors location))
        (if (white? (cadr instructions))
            (set! direction (turn-right direction))
            (set! direction (turn-left direction)))
        (set! location (+ location direction))
        (run))))
  (assert (done? M))
  (values colors visited))

(define (partA)
  (let-values (((_ visited) (painter (cpu intcode) black)))
    (vector-length (hashtable-cells visited))))

(define (display-message colors)
  (let ((zs (map conjugate (vector->list (hashtable-keys colors)))))
    (run-ssvg (zs->ssvg zs) "Day11.svg")))

(define (partB)
  (let-values (((colors _) (painter (cpu intcode) white)))
    (display-message colors)))
